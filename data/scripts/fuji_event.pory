script EventScript_FujiOutside{
    lock
    faceplayer
    msgbox("MR. FUJI: {PLAYER}!\nHow is your quest going?\pAh, you and your POKéMON are\nthe POKéMON LEAGUE CHAMPIONS!\pCongratulations!\nYour deep love for your POKéMON\lis obvious to anyone.\pI have a favor to ask.\pThere is an island far from KANTO\nwhere a certain POKéMON lives.\pThat POKéMON was mistreated by\nhumans in the past.\pI was hoping you would befriend\nthat POKéMON and show it that\lhumans can be kind and gentle.\pWill you do this for me?", MSGBOX_YESNO)
    while(var(VAR_RESULT) == NO)
    {
        msgbox("Helping this POKéMON is very\nimportant to me, {PLAYER}.\pPlease tell me you'll help.", MSGBOX_YESNO)
    }
    msgbox("MR. FUJI: Thank you, {PLAYER}.\pI have an old map to the island.\nI'm sure you've befriended a sailor\lor two during your quest.\pThey will be able to read this\nmap and take you there.", MSGBOX_DEFAULT)
    checkitemspace(ITEM_OLD_SEA_MAP, 1)
    if(var(VAR_RESULT) == FALSE)
    {
        msgbox(LavenderTown_VolunteerPokemonHouse_Text_MustMakeRoomForThis, MSGBOX_DEFAULT)
        release
        end
    }
    giveitem_msg("{PLAYER} received an OLD SEA MAP\nfrom MR. FUJI.$", ITEM_OLD_SEA_MAP, 1, MUS_OBTAIN_KEY_ITEM)
    setflag(FLAG_GOT_OLD_SEA_MAP)
    msgbox("MR. FUJI: I know you will treat that\nPOKéMON with love and kindness.\pThank you, {PLAYER}.", MSGBOX_NPC)
    getplayerxy(VAR_RESULT, VAR_0x8003)
    if(var(VAR_RESULT) != 9)
    {
        applymovement(4, Movement_FujiToHouse)
        waitmovement(0)
    }
    else
    {
        applymovement(4, Movement_FujiToHouseAroundPlayer)
        waitmovement(0)
    }
    opendoor(10, 11)
    waitdooranim
    applymovement(4, PalletTown_Movement_OakEnterLab)
    waitmovement(0)
    closedoor(10, 11)
	waitdooranim
    setflag(FLAG_HIDE_LAVENDER_OUTSIDE_FUJI)
    clearflag(FLAG_HIDE_POKEHOUSE_FUJI)
    release
    end
}

movement Movement_FujiToHouse {
    walk_right * 2
    face_up
}

movement Movement_FujiToHouseAroundPlayer {
    walk_down
    walk_right * 2
    walk_up
}